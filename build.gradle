import groovy.json.JsonBuilder
import groovy.json.JsonSlurper

plugins {
    id 'java-library'
    id("com.github.node-gradle.node") version "7.1.0"
}

node {
    download = false
}

tasks.register("npmBuild", NpmTask) {
    dependsOn(tasks.named('npmInstall'))
    dependsOn(tasks.named('clearDist'))
    description = 'Run \'npm build\' command'
    group = 'npm'
    args = ['run', 'build']
}

tasks.register("runNpmDev", NpmTask) {
    group = "npm"
    args = ["run", "dev"]
}

tasks.register("clearDist", Delete) {
    group = 'npm'
    delete 'dist/'
}
def prettyJson = { Object json ->
    def rawJson = new JsonBuilder(json).toPrettyString()
    def decodedJson = rawJson.replaceAll(/\\u([0-9a-fA-F]{4})/) {
        new String(Character.toChars(Integer.parseInt(it[1], 16)))
    }
    return decodedJson.replace("    ", "  ")
}

tasks.register("syncLangFiles") {
    def langDir = project.file("public/lang")
    doLast {


        def enUSFile = new File(langDir, "en_us.json")
        def enUSContent = new JsonSlurper().parseText(enUSFile.text)

        def jsonFiles = langDir.listFiles({ dir, name -> name.endsWith('.json') && !name.contains("all") } as FilenameFilter)

        jsonFiles.each {
            if (it.name == 'en_us.json' || it.name == 'en_ud.json') return

            def langContent = new JsonSlurper().parseText(it.text)
            boolean modified = false

            def enKeys = enUSContent.keySet()
            def langKeys = langContent.keySet()

            (enKeys - langKeys).each { key ->
                langContent[key] = enUSContent[key]
                modified = true
                println "  [added key] ${it.name} -> $key"
            }

            (langKeys - enKeys).each { key ->
                langContent.remove(key)
                modified = true
                println "  [removed key] ${it.name} -> $key"
            }

            if (modified) {
                def updatedContent = prettyJson(langContent)

                it.write(updatedContent, 'UTF-8')
                println "Updated ${it.name}"
            }
        }
    }
}

def (normalChars, upsideDownChars) = [
        "abcdefghijklmnñopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789()_,;.?!/\\'",
        "ɐqɔpǝɟbɥıظʞןɯuuodbɹsʇnʌʍxʎzⱯᗺƆᗡƎℲ⅁HIſʞꞀWNOԀὉᴚS⟘∩ΛMXʎZ0ƖᄅƐㄣϛ9ㄥ86)(‾'؛˙¿¡/\\,"
]
def charMap = [:]
normalChars.eachWithIndex { c, i -> charMap[c] = upsideDownChars[i] }

def toUpsideDown = { String normal ->
    def ud = new char[normal.length()]
    int i = 0

    while (i < normal.length()) {
        def c = normal[i]
        if (c != '%') {
            def replacement = charMap[c] ?: c
            ud[normal.length() - 1 - i] = replacement
            i++
        } else {
            def fmtArg = new StringBuilder().append(c)
            i++

            while (i < normal.length()) {
                c = normal[i]
                if (Character.isDigit(c as char) || c in ['%', '$', 's', 'd']) {
                    fmtArg.append(c)
                    i++
                } else {
                    break
                }
            }

            def argStr = fmtArg.toString()
            (0..<argStr.length()).each { j ->
                ud[normal.length() - i + j] = argStr[j]
            }
        }
    }
    new String(ud)
}

tasks.register("generateUpsideDown") {
    def langDir = project.layout.projectDirectory.dir("public/lang")
    def sourceFile = langDir.file("en_us.json").asFile
    def targetFile = langDir.file("en_ud.json").asFile

    doLast {
        def enUSContent = new JsonSlurper().parseText(sourceFile.text)

        def enUDContent = enUSContent.collectEntries { key, value ->
            [
                    key, toUpsideDown(value)
            ]
        }

        enUDContent["lang.name"] = toUpsideDown("English(Upside Down)")

        def jsonString = prettyJson(enUDContent)
        targetFile.write(jsonString, "UTF-8")
    }
}
